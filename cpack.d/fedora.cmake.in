##############################################################################
# Unoffical CPACK configuration for Linux Fedora 27 
#
# See also 
# http://fedoraproject.org/wiki/Packaging:Naming
# Tested with rpmlint (https://github.com/rpm-software-management/rpmlint)
cmake_policy(SET CMP0012 NEW)

include("@CMAKE_CURRENT_BINARY_DIR@/commons.cmake")
config_package(NAME "@PROJECT_NAME@" LOWCASE)

# set_filename ([COMPONENT component_name] [ARCH architecture])
# 
# COMPONENT
#   the component name for which to set the package file name. 
#   when passed the CPACK_RPM_${component_name}_FILE_NAME will be set
#
# ARCH
#   the architecture for the generate package file
#   when not passed the CMAKE_SYSTEM_PROCESSOR will be used
#
# builds the package file name according to Fedora rules
macro(set_filename)
  set(oneValueArgs COMPONENT ARCH)
  cmake_parse_arguments(FILE "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN} )
  if (NOT FILE_ARCH)
    set(FILE_ARCH "${CMAKE_SYSTEM_PROCESSOR}")
  endif()
  set(_version_part_ "${CPACK_PACKAGE_VERSION}-${CPACK_RPM_PACKAGE_RELEASE}.${FILE_ARCH}")
  if (FILE_COMPONENT)
    set(CPACK_RPM_${FILE_COMPONENT}_FILE_NAME "${CPACK_RPM_${FILE_COMPONENT}_PACKAGE_NAME}-${_version_part_}.rpm")
  else()
    set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${_version_part_}")
  endif()
endmacro(set_filename)

# this macro define a component package according to Fedora specifications for
# COMPONENT NAME, GROUP and FILE NAME POSTFIX (third optional argument)
macro(component COMPONENT GROUP ARCH)
  if (${ARGC} GREATER 3)
    set(CPACK_RPM_${COMPONENT}_PACKAGE_NAME "${CPACK_PACKAGE_NAME}-${ARGV3}")
  else()
    set(CPACK_RPM_${COMPONENT}_PACKAGE_NAME "${CPACK_PACKAGE_NAME}")
  endif()
  set(CPACK_RPM_${COMPONENT}_PACKAGE_ARCHITECTURE ${ARCH})
  set_filename(COMPONENT "${COMPONENT}" ARCH "${ARCH}")
  set(CPACK_RPM_${COMPONENT}_PACKAGE_GROUP ${GROUP})
endmacro(component)

set(CPACK_GENERATOR             "RPM")
# see https://fedoraproject.org/wiki/Packaging:DistTag?rd=Packaging/DistTag
set(CPACK_RPM_PACKAGE_RELEASE "@PACKAGE_BUILD_NUMBER@.fc27")
set_filename()
set(CPACK_RPM_COMPONENT_INSTALL "ON")
set(CPACK_RPM_PACKAGE_LICENSE   "BSD")
set(CPACK_RPM_PACKAGE_URL       "${PROJECT_URL}")
set(CPACK_RPM_EXCLUDE_FROM_AUTO_FILELIST_ADDITION    "/usr/lib64/cmake")
set(CPACK_RPM_RUNTIME_POST_INSTALL_SCRIPT_FILE       "@CMAKE_CURRENT_SOURCE_DIR@/fedora_post_uninstall.sh")
set(CPACK_RPM_RUNTIME_POST_UNINSTALL_SCRIPT_FILE     "@CMAKE_CURRENT_SOURCE_DIR@/fedora_post_uninstall.sh")
set(CPACK_RPM_DEVELOPMENT_POST_INSTALL_SCRIPT_FILE   "@CMAKE_CURRENT_SOURCE_DIR@/fedora_post_uninstall.sh")
set(CPACK_RPM_DEVELOPMENT_POST_UNINSTALL_SCRIPT_FILE "@CMAKE_CURRENT_SOURCE_DIR@/fedora_post_uninstall.sh")

component(RUNTIME       "System Environment/Libraries" "@CMAKE_SYSTEM_PROCESSOR@" libs)
component(DEVELOPMENT   "Development/Libraries"        "@CMAKE_SYSTEM_PROCESSOR@" devel)
component(DOCUMENTATION "Documentation"                "noarch" doc)
